import os

GENOMES = ['mm10', 'hg19']
GENOME2SPECIES = {'mm10': 'Mus musculus', 'hg19': 'Homo sapiens'}
SUBDIRS = ['downloaded', 'tmp']

RFAM_FASTA_URL = 'ftp://ftp.sanger.ac.uk/pub/databases/Rfam/CURRENT/Rfam.fasta.gz'
GENOME_2BIT_URL = 'http://hgdownload.soe.ucsc.edu/goldenPath/{genome}/bigZips/{genome}.2bit'
REFGENE_URL = 'http://hgdownload.cse.ucsc.edu/goldenPath/{genome}/database/refGene.txt.gz'
KNOWNGENE_URL = 'http://hgdownload.cse.ucsc.edu/goldenPath/{genome}/database/knownGene.txt.gz'

GENBANK_EFETCH_URL = (
    'http://eutils.ncbi.nlm.nih.gov/entrez/eutils/efetch.fcgi?db=nuccore'
    '&id={gi}&rettype=fasta&retmode=text')
RDNA_GENBANK_URLS = {
    'Mus musculus': GENBANK_EFETCH_URL.format(gi=38176281),
    'Homo sapiens': GENBANK_EFETCH_URL.format(gi=555853),
}

# create directories when missing
for genome in GENOMES:
    for subdir in SUBDIRS:
        dirpath = os.path.join('genomes', genome, subdir)
        if not os.path.isdir(dirpath):
            os.makedirs(dirpath)

rule all:
    input: expand('genomes/{genome}/contaminants/contaminants.genomecomp', genome=GENOMES), \
           expand('genomes/{genome}/genome/genome.maps/genome.splicesites.iit', genome=GENOMES)

rule download_rfam_fasta:
    output: 'genomes/{genome}/downloaded/Rfam.fasta.gz'
    shell: 'wget -O {output} {RFAM_FASTA_URL}'

rule download_rDNA:
    output: 'genomes/{genome}/downloaded/rDNA.fa'
    run:
        url = RDNA_GENBANK_URLS[GENOME2SPECIES[wildcards.genome]]
        shell('wget -O "{output}" "{url}"')

rule rfam_rRNA_seq_names:
    input: 'genomes/{genome}/downloaded/Rfam.fasta.gz'
    output: 'genomes/{genome}/tmp/Rfam-rRNA-names'
    run:
        species = GENOME2SPECIES[wildcards.genome]
        shell("zgrep '^>.*rRNA.*{species}' {input} | sed -e 's,^>\([^ ]*\).*,\\1,g' > {output}")

rule make_contaminants_fasta:
    input: rDNA_fasta='genomes/{genome}/downloaded/rDNA.fa', \
           rfam_ids='genomes/{genome}/tmp/Rfam-rRNA-names', \
           rfam_fasta='genomes/{genome}/downloaded/Rfam.fasta.gz'
    output: 'genomes/{genome}/contaminants.fa'
    shell: 'faSomeRecords {input.rfam_fasta} {input.rfam_ids} {output} && \
                (cat {input.rDNA_fasta} files/illumina.fa >> {output})'

rule build_contaminants_gsnap_index:
    input: 'genomes/{genome}/contaminants.fa'
    output: 'genomes/{genome}/contaminants/contaminants.genomecomp'
    shell: 'gmap_build -D genomes/{wildcards.genome} -d contaminants -k 12 -b 12 -q 1 {input}'

rule download_genome_sequence:
    output: twobit='genomes/{genome}/genome.2bit', fasta='genomes/{genome}/genome.fa', \
            fasta_index='genomes/{genome}/genome.fa.fai'
    run:
        genome = wildcards.genome
        shell('wget -O {output.twobit} ' + GENOME_2BIT_URL)
        shell('twoBitToFa {output.twobit} {output.fasta}')
        shell('samtools faidx {output.fasta}')

rule build_gsnap_genome_index:
    input: 'genomes/{genome}/genome.fa'
    output: 'genomes/{genome}/genome/genome.genomecomp'
    threads: 100 # never run multiple builds
    shell: 'gmap_build -D genomes/{wildcards.genome} -d genome -k 12 -b 12 -q 1 {input}'

rule download_refgene:
    output: 'genomes/{genome}/downloaded/refGene.txt.gz'
    run:
        url = REFGENE_URL.format(genome=wildcards.genome)
        shell('wget -O {output} {url}')

rule download_knowngene:
    output: 'genomes/{genome}/downloaded/knownGene.txt.gz'
    run:
        url = KNOWNGENE_URL.format(genome=wildcards.genome)
        shell('wget -O {output} {url}')

rule build_gsnap_splice_index:
    input: refgene='genomes/{genome}/downloaded/refGene.txt.gz', \
           knowngene='genomes/{genome}/downloaded/knownGene.txt.gz', \
           gsnapidx='genomes/{genome}/genome/genome.genomecomp'
    output: 'genomes/{genome}/genome/genome.maps/genome.splicesites.iit'
    shell: '(zcat {input.refgene} | psl_splicesites -s 1; \
             zcat {input.knowngene} | psl_splicesites) | iit_store -o {output}'

